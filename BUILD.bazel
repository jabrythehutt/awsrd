load("@npm//:defs.bzl", "npm_link_all_packages")
load("index.bzl", "cjs_bundle", "cli", "to_package_args_choice")
load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("platforms.bzl", "platforms")
load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_run_binary")
load("@aspect_rules_jest//jest:defs.bzl", "jest_test")
load("@npm//:@vscode/vsce/package_json.bzl", "bin")
load("@aspect_rules_js//npm:defs.bzl", "stamped_package_json")
load("ts_project.bzl", "ts_project")
load("@rules_pkg//:pkg.bzl", "pkg_zip")
load("@aspect_rules_ts//ts:defs.bzl", "ts_config")
load("@npm//:tsconfig-to-swcconfig/package_json.bzl", swcrc_bin = "bin")

package(default_visibility = ["//visibility:public"])

platforms()

session_manager_bin_name = "//session-manager:bin"

npm_link_all_packages(
    name = "node_modules",
)

deps = [
    "//:node_modules/@aws-sdk/client-ec2",
    "//:node_modules/@aws-sdk/client-ec2-instance-connect",
    "//:node_modules/@aws-sdk/client-ssm",
    "//:node_modules/@aws-sdk/client-sts",
    "//:node_modules/@aws-sdk/credential-providers",
    "//:node_modules/@smithy/shared-ini-file-loader",
    "//:node_modules/@smithy/smithy-client",
    "//:node_modules/@smithy/util-stream",
    "//:node_modules/@aws-sdk/region-config-resolver",
    "//:node_modules/@aws-sdk/types",
    "//:node_modules/@types/lodash",
    "//:node_modules/@types/node",
    "//:node_modules/@types/sshpk",
    "//:node_modules/@types/validator",
    "//:node_modules/@types/vscode",
    "//:node_modules/@types/yargs",
    "//:node_modules/aws-cdk-lib",
    "//:node_modules/cdk-monitoring-constructs",
    "//:node_modules/constructs",
    "//:node_modules/lodash",
    "//:node_modules/rxjs",
    "//:node_modules/sshpk",
    "//:node_modules/validator",
    "//:node_modules/yargs",
    "//:node_modules/@types/aws-lambda",
]

tests = ["*.test.ts"]

ts_config(
    name = "tsconfig",
    src = "tsconfig.json",
    visibility = ["//visibility:public"],
)

stamped_package_json(
    name = "package",
    stamp_var = "STABLE_GIT_VERSION",
)

ts_project(
    name = "lib",
    srcs = glob(
        ["*.ts"],
        exclude = tests,
    ) + ["package"],
    deps = deps,
)

ts_project(
    name = "test_lib",
    srcs = glob(tests) + ["package"],
    deps = [
        "lib",
        "//:node_modules/@types/jest",
    ],
)

jest_test(
    name = "test",
    config = "jest.config.mjs",
    data = [
        "package",
        "test_lib",
    ],
    node_modules = "//:node_modules",
    node_options = [
        "--experimental-vm-modules",
    ],
)

entry_base = "ssmProxyScript"

proxy_script_file_name = entry_base + ".cjs"

sourcemap = None

minify = True

external_libs = [
    "vscode",
    "aws-cdk",
]

cjs_bundle(
    name = "ssm_proxy_script",
    entry_point = entry_base + ".js",
    minify = minify,
    output = proxy_script_file_name,
    sourcemap = sourcemap,
    deps = [
        "lib",
    ],
)

cdk_app_file_name = "app.cjs"

cjs_bundle(
    name = "stop_handler",
    entry_point = "stopHandler.js",
    external_libs = ["@aws-sdk/*"],
    minify = minify,
    output = "stopHandler.cjs",
    sourcemap = sourcemap,
)

pkg_zip(
    name = "stopper_bundle",
    srcs = [
        "stop_handler",
    ],
)

cjs_bundle(
    name = "cdk_app",
    define = {
        "process.env.STOPPER_ZIP": '"stopper_bundle.zip"',
        "process.env.STOPPER_HANDLER": '"stopHandler.handler"',
    },
    entry_point = "app.js",
    external_libs = external_libs,
    minify = minify,
    output = cdk_app_file_name,
    sourcemap = sourcemap,
    deps = [
        "lib",
    ],
)

cjs_bundle(
    name = "extension",
    define = {
        "process.env.PROXY_SCRIPT_FILENAME": '"{proxy_script_file_name}"'.format(proxy_script_file_name = proxy_script_file_name),
        "process.env.SESSION_MANAGER_BIN": '"{session_manager_bin_name}"'.format(session_manager_bin_name = session_manager_bin_name),
        "process.env.CDK_APP_FILENAME": '"{cdk_app_file_name}"'.format(cdk_app_file_name = cdk_app_file_name),
    },
    entry_point = "extension.js",
    external_libs = external_libs,
    minify = minify,
    output = "extension.cjs",
    sourcemap = sourcemap,
    deps = [
        "cdk_app",
        "lib",
        "ssm_proxy_script",
    ],
)



npm_package(
    name = "dist",
    srcs = [
        "cdk_app",
        "package",
        "extension",
        "stopper_bundle",
        "ssm_proxy_script",
        "README.md",
        "LICENSE.txt",
        ".vscodeignore",
        session_manager_bin_name,
    ] + glob(["media/*.*"]),
    exclude_srcs_patterns = [],
)

package_file = "package.vsix"

base_args = [
    "package",
    "--no-git-tag-version",
    "--no-update-package-json",
    "--no-dependencies",
    "-o",
    "../" + package_file,
]

bin.vsce(
    name = "vsc_package",
    srcs = [
        "dist",
    ],
    outs = [
        package_file,
    ],
    args = select(to_package_args_choice(base_args)),
    chdir = "$(rootpath dist)",
)

bin.vsce_binary(
    name = "publish",
    args = [
        "publish",
        "--no-git-tag-version",
        "--no-update-package-json",
        "--packagePath",
        "$(rootpath vsc_package)",
    ],
    data = [
        "vsc_package",
    ],
)

swcrc_bin.tsconfig_to_swcconfig(
    name = "swcrc",
    srcs = [
        "tsconfig",
    ],
    outs = [
        ".swcrc",
    ],
    args = [
        "-o",
        ".swcrc",
    ],
)
